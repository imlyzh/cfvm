
module = { "mod" ~ symbol ~ ":" ~ module_bodys ~ EOI }

module_bodys = { module_body* }

module_body =
    { function_def
    | function_decl
    | type_def
    | constant_def
    | variable_def
    }

type_def = {
    is_pub ~ type_define_symbol ~ "=" ~ type_value
}

constant_def = {
    is_pub ~ "const" ~ global_define_symbol ~ ":" ~ type_value ~ "=" ~ const_value
}

variable_def = {
    is_pub ~ "var" ~ global_define_symbol ~ ":" ~ type_value ~ ("=" ~ const_value)?
}

const_value = ${
    "const_value"
}

function_decl = {
    "import" ~ "fn" ~ global_define_symbol ~ function_type ~ ";"
}

function_def = {
    function_attr ~ "fn" ~ global_define_symbol ~
    function_type ~ "{" ~ blocks ~ "}"
}

function_attr = { is_extern ~ is_pub ~ is_inline }

is_extern = { "extern"? }

is_pub = { "pub"? }

is_inline = { ("inline" | "const")? }

// types

function_type = {
    params ~ "->" ~ type_bind_metadata
}

params = {
    "(" ~ (params_pair ~ ("," ~ params_pair)*)? ~ ")"
}

params_pair = {
    params_name ~ ":" ~ type_bind_metadata
}

type_bind_metadata = { type_value ~ ("," ~ alloca_type)? }

params_name = @
    { UNDERLINE
    | local_symbol
}

type_value =
    { type_
    | type_symbol
    }

type_ =
    { void_type
    | first_class_type
    | function_type
    }

void_type = @{ "void" }

first_class_type =
    { opaque_type
    | simple_type
    | array_type
    | record_type
}

opaque_type = { "opaque" }

simple_type =
    { int_type
    | float_type
    | pointer_type
    | vector_type
    }

int_type = @
    { "i1"
    | "i8"
    | "i16"
    | "i32"
    | "i64"
    | "i128"
}

float_type = @
    { "f8"
    | "f16"
    | "f32"
    | "f64"
    | "f128"
    | "ppc_f128"
}

pointer_type = {
    "*" ~ type_
}

vector_type = {
    "<" ~ simple_type ~ ";" ~ number ~ ">"
}

array_type = {
    "[" ~ type_ ~ ";" ~ number ~ "]"
}

record_type = {
    is_not_aligned ~ "{" ~ (record_kv_pair ~ ("," ~ record_kv_pair)*)? ~ "}"
}

is_not_aligned = { "#"? }

record_kv_pair = {
    params_name ~ ":" ~ type_
}

// types end

blocks = {
    basic_init_block ~ basic_block*
}

basic_init_block = {
    inst* ~
    terminator
}

basic_block = {
    label_symbol ~ ":" ~
    inst* ~
    terminator
}

inst =
    { store
    | bind
    | operator
    }

store = {
    "store" ~ is_atomic ~ is_volatile ~ local_symbol ~ value
}

is_volatile = { "volatile"? }

bind = {
    local_symbol ~ "=" ~ is_atomic ~ is_volatile ~ is_mutable ~ operator
}

is_atomic = { "atomic"? }

is_mutable = { "mut"? }

operator =
    { alloca
    | get_ptr
    | load
    | cast
    | add
    | fadd
    | sub
    | fsub
    | mul
    | fmul
    | udiv
    | sdiv
    | urem
    | srem
    | frem
    | shl
    | lshr
    | ashr
    | and
    | or
    | xor
    | get_value
    | get_item
    | set_value
    | set_item
    | trunc
    | zext
    | sext
    | ftrunc
    | fext
    | icmp
    | fcmp
    | phi
    | call
}

alloca = {
    "alloca" ~ alloca_type ~ type_ ~ value?
}

alloca_type = {
    ( alloca_type_reg
    | alloca_type_stack)?
}

alloca_type_reg = {
    is_extend ~ "reg" ~ (reg_enum | reg_range | reg_number)
}

is_extend = { "extend"? }

reg_enum = { "(" ~ reg_number ~ ("," ~ reg_number)* ~ ")" }

reg_range = { reg_number ~ "-" ~ reg_number }

reg_number = { number }

alloca_type_stack = _{ "stack" }


get_ptr = {
    "get-ptr" ~ value
}

load = {
    "load" ~ type_ ~ value
}

cast = {
    "cast" ~ type_ ~ value
}

add = { "add" ~ value ~ "," ~ value }
fadd = { "fadd" ~ value ~ "," ~ value }
sub = { "sub" ~ value ~ "," ~ value }
fsub = { "fsub" ~ value ~ "," ~ value }
mul = { "mul" ~ value ~ "," ~ value }
fmul = { "fmul" ~ value ~ "," ~ value }
udiv = { "udiv" ~ value ~ "," ~ value }
sdiv = { "sdiv" ~ value ~ "," ~ value }
urem = { "urem" ~ value ~ "," ~ value }
srem = { "srem" ~ value ~ "," ~ value }
frem = { "frem" ~ value ~ "," ~ value }
shl = { "shl" ~ value ~ "," ~ value }
lshr = { "lshr" ~ value ~ "," ~ value }
ashr = { "ashr" ~ value ~ "," ~ value }
and = { "and" ~ value ~ "," ~ value }
or = { "or" ~ value ~ "," ~ value }
xor = { "xor" ~ value ~ "," ~ value }

get_value = { "get-value" ~ value ~ index_list }

get_item = { "get-item" ~ value ~ value }

set_item = { "set-item" ~ value ~ index_list ~ value }

set_value = { "set-item" ~ value ~ value ~ value }

trunc = { "trunc" ~ int_type ~ value }
zext = { "zext" ~ int_type ~ value }
sext = { "sext" ~ int_type ~ value }

ftrunc = { "ftrunc" ~ float_type ~ value }
fext = { "fext" ~ float_type ~ value }

icmp = { "icmp" ~ icmp_op ~ value ~ "," ~ value }
fcmp = { "fcmp" ~ fcmp_op ~ value ~ "," ~ value }

icmp_op =
    { "eq"
    | "ne"
    | "sge"
    | "sgt"
    | "sle"
    | "slt"
    | "uge"
    | "ugt"
    | "ule"
    | "ult"
    }

fcmp_op =
    { "false"
    | "oeq"
    | "oge"
    | "ogt"
    | "ole"
    | "olt"
    | "one"
    | "ord"
    | "true"
    | "ueq"
    | "uge"
    | "ugt"
    | "ule"
    | "ult"
    | "une"
    | "uno"
    }

phi = {
    "phi" ~ "[" ~ phi_pair ~ ("," ~ phi_pair)* ~ "]"
}

phi_pair = {
    label_symbol ~ ":" ~ value
}

call = {
    "call" ~ value ~ "(" ~ value ~ ("," ~ value)* ~ ")"
}

index_list = {
    index ~ ("." ~ index)*
}

index =
    { symbol
    | number
    }

terminator =
    { ret
    | branch
    | conds
    | switch
    | unrechable
    }

ret = {
    "ret" ~ value?
}

branch = {
    "br" ~ branch_op ~ value ~ "?" ~ label_symbol ~ "," ~ label_symbol
}

branch_op = {
    ( "if-nil"
    | "if-non-nil")?
    }

conds = {
    "conds" ~ "[" ~ conds_pair ~ ("," ~ conds_pair)* ~ "]"
}

conds_pair = { value ~ ":" ~ label_symbol }

switch = {
    "switch" ~ value ~ "[" ~ switch_pair ~ ("," ~ switch_pair)* ~ "]"
}

switch_pair = { number ~ ":" ~ label_symbol }

unrechable = { "unrechable" }

value =
    { local_symbol
    | global_symbol
    | const_value
    }

global_symbol = ${ (symbol ~ ".")? ~ global_define_symbol }

global_define_symbol = ${ "@" ~ symbol }

local_symbol = ${ "$" ~ symbol }

label_symbol = ${ "%" ~ symbol }

symbol = $
    { _quoted_name
    | _name
    | _id
}

type_symbol = ${
    (symbol ~ ".")? ~ type_define_symbol
}

type_define_symbol = $
    { _quoted_name
    | _name
    | type_id
    }

type_id = _{
    "t" ~ _id
}

_quoted_name = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

_id = @{ ('0'..'9')+ }

_name = $ { (ASCII_ALPHA_LOWER | UNDERLINE) ~ (ASCII_ALPHANUMERIC | UNDERLINE)* }

UNDERLINE = _{ "_" }

// number

number = $
    { number_hex
    | number_oct
    | number_bin
    | number_dec
    }

number_dec = _ { ("+" | "-")? ~ ASCII_DIGIT+ }

number_oct = _ { ("+" | "-")? ~ "0o" ~ ASCII_OCT_DIGIT+ }

number_hex = _ { ("+" | "-")? ~ "0x" ~ ASCII_HEX_DIGIT+ }

number_bin = _ { ("+" | "-")? ~ "0b" ~ ASCII_BIN_DIGIT+ }


WHITESPACE = _ { " "
               | "\t"
               | WHITE_SPACE
               | NEWLINE
               }

COMMENT = _ { "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE? }